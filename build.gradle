group 'callisto'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1-M03'
    ext.dokka_version = '0.9.11'

    repositories {
        mavenCentral()
        jcenter() // Required for Dokka
        maven {
            // Required for kotlin_version 1.1-M03
            url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'project-report'
apply plugin: 'org.jetbrains.dokka'

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        // Required for kotlin_version 1.1-M03
        // Required both here and in the buildscript!
        url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
    }
    maven {
        // Required for jcip
        url 'https://maven.repository.redhat.com/ga/'
    }
    flatDir dirs: "${rootDir}/lib"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Used to remove a lot of Java boilerplate
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.8'

    // Used for the decompiler command line interface
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'

    compile group: 'org.jooq', name: 'jool', version: '0.9.12'

    // Used for StringUtils
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    // Used for checking Preconditions mostly
    compile 'com.google.guava:guava:19.0'

    compile group: 'net.jcip', name: 'jcip-annotations', version: '1.0.0.redhat-8'

    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M3")
    testCompile("org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M3")
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
}

mainClassName = "decompiler.CommandLineDecompiler"

// Create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'decompiler.CommandLineDecompiler'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
