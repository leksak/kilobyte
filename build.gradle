group 'callisto'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir dirs: "${rootDir}/lib"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    
    // Primarily used for functional "monads" such as Either. Java
    // doesn't have real monads.
    compile 'io.atlassian.fugue:fugue:4.3.0'

    // Used to remove a lot of Java boilerplate
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.8'

    // Used for the decompiler command line interface
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'


    // Used for StringUtils
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    // Used for checking Preconditions mostly
    compile 'com.google.guava:guava:19.0'

    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M2")
    
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
}

mainClassName = "decompiler.Decompiler"

// Create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'decompiler.Decompiler'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
