group 'kilobyte'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        // Required for kotlin_version 1.1-M04
        // Required both here and in the buildscript!
        url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
    }
}

buildscript {
    ext.kotlin_version = '1.1-M04'
    ext.dokka_version = '0.9.11'

    repositories {
        mavenCentral()
        jcenter() // Required for Dokka
        maven {
            // Required for kotlin_version 1.1-M04
            url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}

sourceCompatibility = 1.8

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Used to remove a lot of Java boilerplate.
    compileOnly 'org.projectlombok:lombok:1.16.8'

    // Used for the decompiler command line interface
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'

    // Used for StringUtils
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    // Used for checking Preconditions mostly
    compile 'com.google.guava:guava:19.0'

    // Used for the Either monad
    compile 'io.atlassian.fugue:fugue:4.3.0'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0-M3"
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
}

mainClassName = "kilobyte.decompiler.CommandLineDecompiler"

// Create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'kilobyte.decompiler.CommandLineDecompiler'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task simulatorJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'kilobyte.simulator.ui.SimulatorApplication'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
